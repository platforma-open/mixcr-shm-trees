ll := import("@platforma-sdk/workflow-tengo:ll")
smart := import("@platforma-sdk/workflow-tengo:smart")
maps := import("@platforma-sdk/workflow-tengo:maps")

json := import("json")

_P_COLUMN_DATA_RESOURCE_MAP := { Name: "PColumnData/ResourceMap", Version: "1" }

groupDataByDonorId := func(donorColumn, datasets, firstDatasetSpec) {
    // we need to form a pColumn with two axes:
	//    axes[0]: donorId
	//    axes[1]: sampleId
	//    axes[2]: mixcrClonotypingBlockId
	//      value: fileRef resource

	// we have:
	//    column of donorIds:
	//        axes[0]: sampleId
	//          value: donorId
	//    several columns of clns:
	//        axes[0]: sampleId
	//          value: fileRef resource

    donorColumnSpec := donorColumn.spec

    trace := pSpec.makeTrace(firstDatasetSpec)

    sampleIdAxis := donorColumnSpec.axesSpec[0]

    resultSpec := trace.inject(maps.clone({
        kind: "PColumn",
        name: "mixcr.com/clns",
        valueType: "File",

        // annotations and domain could differ between datasets
        axesSpec: [
            {
                name: donorColumnSpec.name,
                type: donorColumnSpec.valueType,
                domain: donorColumnSpec.domain,
                annotations: donorColumnSpec.annotations
            },
            sampleIdAxis,
            {
                name: "pl7.app/block",
                type: "String",
                annotations: {
                    "pl7.app/label": "Clonotyping block id"
                }
            }
        ]
    }, { removeUndefs: true }))

    // creating sample to donor map

    sampleToDonor := {}

    // columns with meta could be fetched as data directly
	for k, v in donorColumn.data.getDataAsJson()["data"] {
        sampleId := json.decode(k)[0]
        sampleToDonor[sampleId] = v
	}

    // build pColumn by hand
    dataBuilder := smart.structBuilder(_P_COLUMN_DATA_RESOURCE_MAP, json.encode({ keyLength: 3 }))

    // collect all the clns files that we have into pColumn
	for clonotypingBlockId, dataset in datasets {
		for sKey, fileRef in dataset.data.inputs() {
            sampleId := json.decode(sKey)[0]
            donor := sampleToDonor[sampleId]
            dataBuilder.createInputField(json.encode([donor, sampleId, clonotypingBlockId])).set(fileRef)
		}
    }

    return {
        spec: resultSpec,
        data: dataBuilder.lockAndBuild()
    }
}

// to use the file as a library, we should explicitly export functions
export ll.toStrict({
	groupDataByDonorId: groupDataByDonorId
})
