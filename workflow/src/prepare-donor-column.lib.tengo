ll := import("@platforma-sdk/workflow-tengo:ll")
smart := import("@platforma-sdk/workflow-tengo:smart")

json := import("json")

_P_COLUMN_DATA_RESOURCE_MAP := { Name: "PColumnData/ResourceMap", Version: "1" }

groupDataByDonorId := func(donorColumn, datasets) {
    // we need to form a pColumn with two axes:
	//    axes[0]: donorId
	//    axes[1]: sampleId
	//    axes[2]: mixcrBlockId
	//      value: fileRef resource

	// we have:
	//    column of donorIds:
	//        axes[0]: sampleId
	//          value: donorId
	//    several columns of clns:
	//        axes[0]: sampleId
	//          value: fileRef resource

    donorColumnSpec := donorColumn.get("spec").getDataAsJson()
    
    resultSpec := {
        // annotations and domain could differ between datasets
        "axesSpec": [
            {
                "annotations": donorColumnSpec["annotations"],
                "domain": donorColumnSpec["domain"],
                "name": donorColumnSpec["name"],
                "type": donorColumnSpec["valueType"]
            },
            donorColumnSpec["axesSpec"][0],
            {
                "annotations": {
                    "pl7.app/label": "Clonotyping block id"
                },
                "name": "pl7.app/blockId",
                "type": "String"
            }
        ],
        "kind": "PColumn",
        "name": "mixcr.com/clns",
        "valueType": "File"
    }
    

    sampleToDonor := {}

    // columns with meta could be fetched as data direcctly
	for k, v in donorColumn.get("data").getDataAsJson()["data"] {
        sampleId := json.decode(k)[0]
        sampleToDonor[sampleId] = v
	}

    // build pColumn by hand
    dataBuilder := smart.structBuilder(_P_COLUMN_DATA_RESOURCE_MAP, json.encode({ keyLength: 3 }))

    // collect all the clns files that we have into pColumn
	for blockId, dataset in datasets {
		for sKey, fileRef in dataset.get("data").inputs() {
            sampleId := json.decode(sKey)[0]
            donor := sampleToDonor[sampleId]
            dataBuilder.createInputField(json.encode([donor, sampleId, blockId])).set(fileRef)
		}
    }

    return {
        spec: resultSpec,
        data: dataBuilder.lockAndBuild()
    }
}

// to use the file as a library, we should explicitly export functions
export ll.toStrict({
	groupDataByDonorId: groupDataByDonorId
})
