self := import("@platforma-sdk/workflow-tengo:tpl")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")
ll := import("@platforma-sdk/workflow-tengo:ll")
assets := import("@platforma-sdk/workflow-tengo:assets")
exec := import("@platforma-sdk/workflow-tengo:exec")
times := import("times")

json := import("json")

// for usage in aggregate function, we should specify all outputs that will be used
self.defineOutputs(
    "trees", "treeNodes", "treeNodesWithClones", "treeNodesUniqueIsotype",
    "tsvs",
    "allelesLog", "treesLog",
    "allelesReport", "treesReport",
    "allelesReportJson", "treesReportJson"
)

//// import function for aggregating by-nodes output to make it uniquely addressable by it's native key
tablesAggregation := import(":tablesAggregation")

// import MiXCR as a software to use
mixcrSw := assets.importSoftware("@platforma-open/milaboratories.software-mixcr:main")

// used to postprocess some tables
paggregateSw := assets.importSoftware("@platforma-open/milaboratories.software-ptransform:main")

// env for MiXCR to format progress messages
progressPrefix := "[==PROGRESS==]"

self.body(func(inputs) {
	inputData := inputs[pConstants.VALUE_FIELD_NAME]
    globalParams := inputs.globalParams
    datasetTypes := globalParams.datasetTypes
    downsampling := globalParams.downsampling
    library := inputs.library
    libraryFormat := globalParams.libraryFormat

    ll.assert(!is_undefined(datasetTypes), "datasetTypes undefined")

    addLibraryFile := func(cmdBuilder) {
        if !is_undefined(library) {
            if libraryFormat == "repseqio.json.gz" {
                cmdBuilder.addFile("library.json.gz", library)
            } else {
                cmdBuilder.addFile("library.json", library)
            }
        }
    }

    // seed := times.time_string(times.now())

    allelesCmdBuilder := exec.builder().
        printErrStreamToStdout().
        secret("MI_LICENSE", "MI_LICENSE").
        env("MI_PROGRESS_PREFIX", progressPrefix).
        // env("SEED", seed).
        software(mixcrSw).
        arg("findAlleles").
        arg("--report").arg("report.txt").
        saveFile("report.txt").
        arg("--json-report").arg("report.json").
        saveFile("report.json").
        // template specifies where result files will be written
        arg("--output-template").arg("alleles/{file_name}.clns")

    addLibraryFile(allelesCmdBuilder)

    if !is_undefined(globalParams.seed) {
        allelesCmdBuilder.env("SEED", globalParams.seed)
    }

    toProcess := []
    for sKey, inputFile in inputData.inputs() {
        key := json.decode(sKey)
        sampleId := key[0]
        clonotypingBlockId := key[1]
        // file name should encode axis values. It will be parsed by xsv.importFileMap afterwards to restore axis for clones data
        fileName := sampleId + "___" + clonotypingBlockId + ".clns"
        toProcess = append(toProcess, {
            clonotypingBlockId: clonotypingBlockId,
            sampleId: sampleId,
            fileName: fileName,
            input: inputFile
        })
    }

    for input in toProcess {
        allelesCmdBuilder.addFile(input.fileName, input.input).
            arg(input.fileName).
            saveFile("alleles/" + input.fileName)
    }

    alleles := allelesCmdBuilder.run()

    for input in toProcess {
        input.alleles = alleles.getFile("alleles/" + input.fileName)
    }

    if !is_undefined(downsampling) {
        downsamplingParam := ""
        if downsampling.type == "CountReadsFixed" {
            downsamplingParam = "count-reads-fixed-" + string(downsampling.number)
        } else if downsampling.type == "CountMoleculesFixed" {
            downsamplingParam = "count-molecule-fixed-" + string(downsampling.number)
        } else if downsampling.type == "TopClonotypesByReads" {
            downsamplingParam = "top-reads-" + string(downsampling.number)
        } else if downsampling.type == "TopClonotypesByMolecules" {
            downsamplingParam = "top-molecule-" + string(downsampling.number)
        } else if downsampling.type == "CumulativeTopClonotypesByReads" {
            downsamplingParam = "cumtop-reads-" + string(downsampling.percent)
        } else if downsampling.type == "CumulativeTopClonotypesByMolecules" {
            downsamplingParam = "cumtop-molecule-" + string(downsampling.percent)
        } else {
            ll.panic("Unknown downsampling type: " + downsampling.type)
        }

        for input in toProcess {
            if datasetTypes[input.clonotypingBlockId] == "bulk" {
                downsamplingCmdBuilder := exec.builder().
                    printErrStreamToStdout().
                    secret("MI_LICENSE", "MI_LICENSE").
                    env("MI_PROGRESS_PREFIX", progressPrefix).
                    software(mixcrSw).
                    arg("downsample").
                    arg("--downsampling").
                    arg(downsamplingParam).
                    arg("clones.clns").
                    addFile("clones.clns", input.alleles).
                    saveFile("clones.downsampled.clns")
                addLibraryFile(downsamplingCmdBuilder)
                downsamplingCmd := downsamplingCmdBuilder.run()
                input.alleles = downsamplingCmd.getFile("clones.downsampled.clns")
            }
        }
    }

    shmTreesCmdBuilder := exec.builder().
        printErrStreamToStdout().
        secret("MI_LICENSE", "MI_LICENSE").
        env("MI_PROGRESS_PREFIX", progressPrefix).
        software(mixcrSw).
        arg("findShmTrees").
        arg("--report").arg("report.txt").
        saveFile("report.txt").
        arg("--json-report").arg("report.json").
        saveFile("report.json")

    if !is_undefined(globalParams.seed) {
        shmTreesCmdBuilder.env("SEED", globalParams.seed)
    }

    addLibraryFile(shmTreesCmdBuilder)

    for input in toProcess {
        shmTreesCmdBuilder.
            addFile(input.fileName, input.alleles).
            arg(input.fileName)
    }

    shmTreesCmdBuilder.arg("output.shmt").saveFile("output.shmt")

    shmTrees := shmTreesCmdBuilder.run()
    outputShmt := shmTrees.getFile("output.shmt")

    // export trees without nodes
    shmTreeExportsCmdBuilder := exec.builder().
        printErrStreamToStdout().
        dontSaveStdoutOrStderr().
        inLightQueue().
        secret("MI_LICENSE", "MI_LICENSE").
        software(mixcrSw).
        arg("exportShmTrees")

    addLibraryFile(shmTreeExportsCmdBuilder)

    for arg in inputs.shmTreeTableArgs {
        shmTreeExportsCmdBuilder = shmTreeExportsCmdBuilder.arg(arg)
    }

    shmTreeExportsCmdBuilder = shmTreeExportsCmdBuilder.
        arg("input.shmt").
        arg("output.tsv").
        addFile("input.shmt", outputShmt).
        saveFile("output.tsv")

    shmTreeExports := shmTreeExportsCmdBuilder.run()
    shmTreeTsvRaw := shmTreeExports.getFile("output.tsv")

    shmTreeTsv := tablesAggregation.ensureUniqueness(shmTreeTsvRaw, inputs.shmTreeTableOptions.pfconvParams, "max_by", "totalReadsCountInTree")

    // export tree nodes with data uniq for the node
    shmTreeNodesExportsCmdBuilder := exec.builder().
        printErrStreamToStdout().
        dontSaveStdoutOrStderr().
        inLightQueue().
        secret("MI_LICENSE", "MI_LICENSE").
        software(mixcrSw).
        arg("exportShmTreesWithNodes")

    addLibraryFile(shmTreeNodesExportsCmdBuilder)

    for arg in inputs.shmTreeNodesTableOptions.cmdArgs {
        shmTreeNodesExportsCmdBuilder = shmTreeNodesExportsCmdBuilder.arg(arg)
    }

    shmTreeNodesExportsCmdBuilder = shmTreeNodesExportsCmdBuilder.
        arg("input.shmt").
        arg("output.tsv").
        addFile("input.shmt", outputShmt).
        saveFile("output.tsv")

    shmTreeNodesExports := shmTreeNodesExportsCmdBuilder.run()
    shmTreeNodesTsvRaw := shmTreeNodesExports.getFile("output.tsv")

	shmTreeNodesTsv := tablesAggregation.ensureUniqueness(shmTreeNodesTsvRaw, inputs.shmTreeNodesTableOptions.pfconvParams, "first")

    // export nodes with clones. For each node could be several clones
    shmTreeNodesWithClonesExportsCmdBuilder := exec.builder().
        printErrStreamToStdout().
        dontSaveStdoutOrStderr().
        inLightQueue().
        secret("MI_LICENSE", "MI_LICENSE").
        env("MI_PROGRESS_PREFIX", progressPrefix).
        software(mixcrSw).
        arg("exportShmTreesWithNodes").
        // don't export nodes that don't have clones
        arg("--only-observed")

    addLibraryFile(shmTreeNodesWithClonesExportsCmdBuilder)

    for arg in inputs.shmTreeNodesWithClonesTableArgs {
        shmTreeNodesWithClonesExportsCmdBuilder = shmTreeNodesWithClonesExportsCmdBuilder.arg(arg)
    }

    shmTreeNodesWithClonesExportsCmdBuilder = shmTreeNodesWithClonesExportsCmdBuilder.
        arg("input.shmt").
        arg("output.tsv").
        addFile("input.shmt", outputShmt).
        saveFile("output.tsv")

    shmTreeNodesWithClonesExports := shmTreeNodesWithClonesExportsCmdBuilder.run()
    shmTreeNodesWithClonesTsvRaw := shmTreeNodesWithClonesExports.getFile("output.tsv")

    shmTreeNodesWithClonesTsv := tablesAggregation.ensureUniqueness(shmTreeNodesWithClonesTsvRaw, inputs.shmTreeNodesWithClonesTableOptions.pfconvParams, "max_by", "readCount")
    shmTreeNodesUniqueIsotypeTsv := tablesAggregation.ensureUniqueness(shmTreeNodesWithClonesTsv, inputs.shmTreeNodesUniqueIsotypeTableOptions.pfconvParams, "max_by", "readCount")

    return {
        trees: shmTreeTsv,
        treeNodes: shmTreeNodesTsv,
        treeNodesWithClones: shmTreeNodesWithClonesTsv,
        treeNodesUniqueIsotype : shmTreeNodesUniqueIsotypeTsv,

        tsvs: {
            trees: shmTreeTsv,
            treeNodes: shmTreeNodesTsv,
            treeNodesWithClones: shmTreeNodesWithClonesTsv,
            treeNodesUniqueIsotype : shmTreeNodesUniqueIsotypeTsv
        },

        allelesLog: alleles.getStdoutStream(),
        treesLog: shmTrees.getStdoutStream(),

        allelesReport: alleles.getFile("report.txt"),
        treesReport: shmTrees.getFile("report.txt"),

        allelesReportJson: alleles.getFile("report.json"),
        treesReportJson: shmTrees.getFile("report.json")
    }
})
